load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_push", "oci_load")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//:bzl/transition.bzl", "multi_arch")

package(default_visibility = ["//visibility:public"])

cc_binary(
    name = "hello-world",
    srcs = ["main.cpp"],
)

# Packaging the binary into tar, which is needed by oci_image rule
pkg_tar(
    name = "tar",
    srcs = [":hello-world"],
)

# Making image
# C++ programs usually need some fundamental libraries such as glibc, libstdc++, etc.
# Correspondigly, use language-specific distroless images.
# Here we use docker.io/library/ubuntu image for this C++ program.
oci_image(
    name = "image",
    base = "@docker_lib_ubuntu",
    tars = [":tar"],
    entrypoint = ["/hello-world"],
)

multi_arch(
    name = "images",
    image = ":image",
    platforms = [
        "//bzl/platforms:linux-x86_64",
        "//bzl/platforms:linux-aarch64",
    ],
)

oci_image_index(
    name = "multiarch_image",
    images = [
        ":images"
    ],
)

oci_push(
    name = "push_multiarch_image",
    image = ":multiarch_image",
    remote_tags = ["latest"],
    repository = "ghcr.io/alemoreno991/multiarch/hello-world",
)

# Use with 'bazel run' to load the oci image into a container runtime.
# The image is designated using `repo_tags` attribute.
oci_load(
    name = "load_multiarch_image",
    image = ":multiarch_image",
    format = "oci",
    repo_tags = ["hello-world-ale:latest"],
)
